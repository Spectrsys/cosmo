#!/usr/bin/perl -w
# -*- Mode: Perl; indent-tabs-mode: nil; -*-
# 
# Copyright 2007 Open Source Applications Foundation
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

use strict;

use Cosmo::MC ();
use File::Basename ();
use Getopt::Long ();

BEGIN { $0 = File::Basename::basename($0) }

use constant VERSION => '0.01';
use constant PROGRAM => $0;
use constant USAGE => <<EOT;
Usage: $0 [OPTIONS...] -u [username] -p [password]

An interactive command shell for interacting with Cosmo over Morse Code.

Options:
  -s [url]         server root URL (defaults to http://localhost:8080)
  -u [username]    username of user making the request
  -p [password]    password of user making the request
  -i [uuid]         uuid of the working collection
  -d               print debugging information to STDOUT
  -h               list available command line options (this page)
  -v               print version information and exit

Report bugs to cosmo-dev\@osafoundation.org
EOT

use constant DEFAULT_SERVER_URL => 'http://localhost:8080/cosmo';

my ($ServerUrl, $Username, $Password, $Uuid, $Debug, $opt_help, $opt_version);

# process command line options
Getopt::Long::GetOptions(
    "s=s" => \$ServerUrl,
    "u=s" => \$Username,
    "p=s" => \$Password,
    "i=s" => \$Uuid,
    "d"   => \$Debug,
    "h"   => \$opt_help,
    "v"   => \$opt_version,
    );
(print USAGE and exit) if $opt_help;
(print sprintf("%s/%s\n", PROGRAM, VERSION) and exit) if $opt_version;

(print USAGE and exit) unless $Username && $Password;

$ServerUrl ||= DEFAULT_SERVER_URL;
chop($ServerUrl) if $ServerUrl =~ m|/$|;

Cosmo::MC::Shell->new($ServerUrl, $Username, $Password, $Uuid, $Debug)->
    cmdloop();

exit;

package Cosmo::MC::Shell;

use IO::File ();
use Term::Shell ();
use XML::XPath ();

use base qw(Term::Shell);

sub init {
    my $self = shift;

    my $serverUrl = $self->{API}{args}[0];
    my $username = $self->{API}{args}[1];
    my $password = $self->{API}{args}[2];
    my $uuid = $self->{API}{args}[3];
    my $debug = $self->{API}{args}[4];

    print "Morse Code command shell v", main::VERSION, "\n";
    print "Debugging enabled.\n" if $debug;

    $self->{mc} = Cosmo::MC->new($serverUrl, $username, $password, $debug);
    $self->{debug} = $debug;
    $self->{uuid} = $uuid;
    $self->{token} = undef;

    $self->{mc}->agent(main::PROGRAM . "/" . main::VERSION);

    eval {
        $self->{mc}->check_server_availability();
        print "Connected to $serverUrl.\n";
    };
    if ($@) {
        print "Error: $@" if $self->{debug};
        print "Failed to connect to $serverUrl. Exiting.\n";
        exit 1;
    }
}

sub prompt_str { "mc> " }
sub alias_exit { ("quit") }

sub run_uuid {
    my $self = shift;
    my $uuid = shift;

    $self->{uuid} = ($uuid ne "clear" ? $uuid : undef) if $uuid;

    print "Working uuid: $self->{uuid}\n" if $self->{uuid};
    print "No working uuid set.\n" unless $self->{uuid};
}
sub help_uuid { "Displays the working collection uuid. If an argument is provided, sets the working uuid, unless the argument is 'clear', which unsets the working uuid..\n" }
sub smry_uuid { "get/set the working collection uuid" }

sub run_token {
    my $self = shift;

    print "Working token: $self->{token}\n" if $self->{token};
    print "No working token set.\n" unless $self->{token};
}
sub help_token { "Displays the working sync token.\n" }
sub smry_token { "get the working sync token" }

sub run_subscribe {
    my $self = shift;

    do {
        print "Working collection uuid not set.\n";
        return;
    } unless $self->{uuid};

    warn "Subscribing to collection $self->{uuid}.\n" if $self->{debug};
    my ($xml, $token) = eval { $self->{mc}->subscribe($self->{uuid}); };
    if ($@) {
        print "Failed: $@\n";
        return;
    }

    my $xpath = XML::XPath->new(xml => $xml);
    my $recordsets = $xpath->findnodes('/eim:collection/eim:recordset');
    print "Found ", ($recordsets->size() == 1 ? "1 recordset.\n" :
                     $recordsets->size() . " recordsets.\n");

    $self->{token} = $token;
    print "Received token $token.\n";
}
sub help_subscribe { "Subscribes to the collection specified by the working uuid.\n" }
sub smry_subscribe { "subscribe to the working collection" }

sub run_publish {
    my $self = shift;
    my $filename = shift or do {
        print "Name of EIMML file must be specified.\n";
        return;
    };

    do {
        print "Working collection uuid not set.\n";
        return;
    } unless $self->{uuid};

    my $fh = IO::File->new($filename) or do {
        print "Cannot open $filename: $!\n";
        return;
    };

    warn "Publishing collection $self->{uuid} from $filename.\n"
        if $self->{debug};
    my $token = eval { $self->{mc}->publish($self->{uuid}, $fh); };
    if ($@) {
        print "Failed: $@\n";
        return;
    }

    $fh->close();

    $self->{token} = $token;
    print "Received token $token.\n";
}
sub help_publish { "Publishes a collection based on the contents of an EIMML file and sets its uuid as the working uuid. The file's location is given as an argument.\n" }
sub smry_publish { "publish a collection" }

1;
